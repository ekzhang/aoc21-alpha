(defun read-rules ()
  (read-line)
  (loop for line = (read-line *standard-input* nil) while line
    collect (cons (subseq line 0 2) (char line 6))))

(defun interleave (l1 l2)
  (if l2
    `(,(car l1) ,(car l2) ,@(interleave (cdr l1) (cdr l2)))
    l1))

(defun seq-pairs (seq)
  (loop for i from 0 to (- (length seq) 2)
    collect (subseq seq i (+ i 2))))

(defun polymerize (seq rules)
  (format nil "狺ㄩ铘弪戾狯ㄣ镥蜚箦ъ轶舂磲疸狎灬礅溽疳舂ㄣ潋ㄡ篌镢疳蝓戾呼弩＇篝蜷铉僵┅箦癍疳轵箦瘵┅┅ㄤ彐躅狃痨ㄩ癌ㄡ痧禊ū瞟ㄦ躅汜祆┅┅语躜沐梏麴蠛篝徙腼鲥蜴祜鳟泔懑岑栋当车腐驳贝彻ㄤ彐躅泔礅轭瀛泔躅趔ㄡ扉篝祜镳鏖翳栳箬磲脲栳箬翎忪呼弩＇羼踽飑骘脲鲠祯濠轭犰轶滹ㄩ钽ㄧ弭栳箬脲栳箬癌鲠祯濠骈钺祆蝈趱蝾祜镳骘脲忮轭遽汨栳箬脲镦栳箬躞轭ㄨ狍璀鲠祯鲠祯濠泔祆邈ㄣ镱脲鲠祯濠┅┅ㄤ彐躅泔躅舡躅轳蹂ㄡ扉篝ㄣ镯忾铄泔躅趔磲疸狎灬礅溽脲ㄣ镱脲暴犰轶舂┅ㄤ彐躅泔躅舡疳轵箦瘵ㄣ秕铘躅轳蹂箦癍疳轵箦瘵┅ㄤ彐躅痫禊礤蜷瀛疳轵ㄣ秕铘蝓戾螬物翦磲疸犷轶扉脲骒狒歪皎轭骢钽糸镱犰痱镧蜥眄轭灬铉踽珏螽ㄣ镯忾铄泔躅趔磲疸犷灬礅溽ㄤ弩趄蹉趱蜷铉忾钿疳鲠飑戾èㄣ潋ㄡ篌镢疳蝓戾呼弩＇篝蜷铉僵┅扉篝ㄣ镱ㄦ矧磲铋狺幄ㄣ栳疳癌悌鲠飑ㄣ镱ㄦ矧磲铋狺幄ㄣ栳疳暴鲠飑┅┅泔躅趔┅ㄤ彐躅疳轵蟓俱栳蝮ㄣ秕铘箦瘵ㄣ镯忾铄泔躅趔ㄣ镱ㄣ镱ㄣ栳箦癌暴泔躅翳骈蝮汨狎徙翦镱沐磲疸狎灬礅溽ㄣ镱ㄣ栳ㄣ狎暴ㄣ潋┅泔躅趔┅┅ㄤ彐躅泔躅趔蜥铉ㄣ秕铘螬戾è箫螋邃泔躅趔箫螋磲疸狎с潋泔躅趔Ъ┅ōㄣ狎灬篝箫螋邃泔躅趔┅ㄣ狎箫螋邃泔躅趔┅┅ㄤ彐躅磲轭ī戾舄è箦蝈徜扉铄┅蝓戾蝈徜蝓戾螬┅嗅螋戾舄è铄鳝箦ㄡ痧禊卑灬礅溽箦瘵痫禊礤蜷箦蝓戾螬箦瘵ㄣ秕铘ㄣ秕铘躅轳蹂ㄣ镥蜚铄鳝箦ъ轶舂┅ㄦ矧磲狺ア铄鳝箦瘵ㄦ矧磲狺" counts)
      (format t "a%" (counts-range counts)))
    ; Part 2
    (let* ((pairs (count-pairs seq))
           (new-pairs (apply-n 40 (lambda (x) (polymerize-pairs x rules)) pairs))
           (counts (pairs->chars new-pairs seq)))
      ; (format t "狺" counts)
      (format t "a%" (counts-range counts)))))

(main)
